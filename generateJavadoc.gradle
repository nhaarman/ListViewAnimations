buildscript {

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.ajoberstar:gradle-git:0.8.+'
    }

}

import org.ajoberstar.grgit.*
def repo = Grgit.open(".")

android.libraryVariants.all { variant ->
    task("generate${variant.name}Javadoc", type: Javadoc) {
        def branch = repo.branch.current.name
        if(project.hasProperty('BRANCH')) {
            branch = BRANCH
        }

        title = POM_NAME + " (" + branch + ")"
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        destinationDir file("./build/docs/javadoc/" + branch)
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        options.linksOffline("http://d.android.com/reference", System.getenv('ANDROID_HOME') + "docs/reference")
        options.setMemberLevel(JavadocMemberLevel.PACKAGE)
        exclude '**/BuildConfig.java '
        exclude '**/R.java'
    }
}